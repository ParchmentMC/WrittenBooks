plugins {
    id 'java-gradle-plugin'
    id 'org.parchmentmc.writtenbooks' version '0.+'
    id 'maven-publish'
}

project.group = 'org.parchmentmc'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.eclipse.jgit:org.eclipse.jgit:5.13.0.202109080827-r'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.1'
}

writtenbooks {
    githubRepo = 'ParchmentMC/WrittenBooks'
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(8)
    withSourcesJar()
}

test {
    useJUnitPlatform()
}

gradlePlugin {
    // Define the plugin
    plugins.register("writtenbooks") {
        id = 'org.parchmentmc.writtenbooks'
        implementationClass = 'org.parchmentmc.writtenbooks.WrittenBooksPlugin'
    }
}

publishing {
    publications.withType(MavenPublication).configureEach {
        pom {
            name = 'WrittenBooks'
            description = 'Gradle plugin for managing CI-related menial tasks.'
            organization {
                name = 'ParchmentMC'
                url = 'https://github.com/ParchmentMC'
            }
            licenses {
                license {
                    name = 'MIT'
                    url = 'https://opensource.org/licenses/MIT'
                }
            }
        }
    }
}